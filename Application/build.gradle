apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'android-apt'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'maven'
apply plugin: 'io.fabric'

def Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def env = System.getenv()

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    // Version Info
    def versionMajor = 0
    def versionMinor = 1
    def versionPatch = 1
    def versionBuild = 6 // bump for dogfood builds, public betas, etc.
    
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 22
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
    }

    sourceSets {
        main {
            aidl.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res',
                           'src/main/res/images',
                           'src/main/res/layouts',
                           'src/main/res/layouts/activity',
                           'src/main/res/layouts/fragment',
                           'src/main/res/layouts/dialog',
                           'src/main/res/layouts/adapter',
                           'src/main/res/layouts/common'
            ]
        }
    }

    signingConfigs {
        release {
            if (project.rootProject.file('local.properties').exists()) {
//              Environment Variables
                def env_keystore_password = env["KEYSTORE_PASSWORD"]
                def env_key_alias = env["KEY_ALIAS"]
                def env_key_password = env["KEY_PASSWORD"]
//              Local build values
                def keystore_password = properties.getProperty('KEYSTORE_PASSWORD',
                        env_keystore_password)
                def key_alias = properties.getProperty('KEY_ALIAS', env_key_alias)
                def key_password = properties.getProperty('KEY_PASSWORD', env_key_password)
//              Signed release commands
                storeFile file("proxy.jks")
                storePassword keystore_password
                keyAlias key_alias
                keyPassword key_password
            }
        }
    }

    def env_google_tracker = env["GA_TRACKER_ID"]
    def tracker_id = properties.getProperty('GA_TRACKER_ID', env_google_tracker)
    def env_firebase_endpoint = env["FIREBASE_ENDPOINT"]
    def firebase_endpoint = properties.getProperty('FIREBASE_ENDPOINT', env_firebase_endpoint)
    def env_firebase_shared_endpoint = env["FIREBASE_SHARED_ENDPOINT"]
    def firebase_shared_endpoint =
            properties.getProperty('FIREBASE_SHARED_ENDPOINT', env_firebase_shared_endpoint)
    buildTypes {
        debug {
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
            //disable google analytics
            buildConfigField "boolean", "USE_GOOGLE_ANALYTICS", "false"
            buildConfigField "String", "GA_TRACKER_ID", tracker_id
            buildConfigField "String", "FIREBASE_ENDPOINT", firebase_endpoint
            buildConfigField "String", "FIREBASE_SHARED_ENDPOINT", firebase_shared_endpoint
        }
        release {
//            minifyEnabled true
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
            buildConfigField "boolean", "USE_GOOGLE_ANALYTICS", "true"
            buildConfigField "String", "GA_TRACKER_ID", tracker_id
            buildConfigField "String", "FIREBASE_ENDPOINT", firebase_endpoint
            buildConfigField "String", "FIREBASE_SHARED_ENDPOINT", firebase_shared_endpoint

            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError true
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
//        duplicate files in Butterknife and Realm?
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    def isTravis = "true".equals(System.getenv("TRAVIS"))
    def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

    dexOptions {
        javaMaxHeapSize "4g"
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }
}

afterEvaluate {
    def commentMessage = "This is autogenerated crashlytics property from system environment to prevent key to be committed to source control."
    def crash_key = properties.getProperty('CRASHLYTICS_API_KEY', env["CRASHLYTICS_API_KEY"])
    def crash_secret = properties.getProperty('CRASHLYTICS_API_SECRET', env["CRASHLYTICS_API_SECRET"])
    ant.propertyfile(file: "crashlytics.properties", comment: commentMessage) {
        entry(key: "apiSecret", value: crash_secret)
        entry(key: "apiKey", value: crash_key)
    }
}

dependencies {
    //Local
    compile fileTree(dir: 'libs', include: '*.jar')
    //Google
    compile 'com.google.android.gms:play-services-plus:7.5.0'
    compile 'com.google.android.gms:play-services-analytics:7.5.0'
    compile 'com.google.android.gms:play-services-identity:7.5.0'
    //Android
    compile 'com.android.support:design:+'
    compile 'com.android.support:support-v4:+'
    compile 'com.android.support:appcompat-v7:+'
    compile 'com.android.support:recyclerview-v7:+'
    compile 'com.android.support:palette-v7:+'
    //Square
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    compile 'com.jakewharton:butterknife:7.0.0'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    //Third party
    compile 'com.facebook.android:facebook-android-sdk:4.1.0'
    compile 'com.github.frankiesardo:auto-parcel:0.3'
    apt 'com.github.frankiesardo:auto-parcel-processor:0.3'
    compile 'io.realm:realm-android:0.81.1'
    compile 'com.firebase:firebase-client-android:2.3.0'
    compile 'io.reactivex:rxjava:1.0.11'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.caverock:androidsvg:1.2.2-beta-1'
    //Bug Tracking
    compile('com.crashlytics.sdk.android:crashlytics:2.4.0@aar') {
        transitive = true;
    }
}
